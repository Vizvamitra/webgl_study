<!DOCTYPE html>
<html>
<head>
  <title>WebGL - Textures</title>

  <script id="vertex-shader" type="x-shader/x-vertex">
    struct Light{
      vec4 position;
      vec4 ambient;
      vec4 diffuse;
      vec4 specular;
    };

    attribute vec4 vPosition;
    attribute vec4 vNormal;
    attribute vec2 vTexCoord;
    attribute vec4 vTangent;
    attribute vec4 vBitangent;
    
    uniform mat4 uModelMatrix;
    uniform mat4 uViewMatrix;
    uniform mat4 uProjMatrix;

    varying vec3 fPosition;
    varying vec2 fTexCoord;
    varying vec4 fNormal;
    varying vec4 fTangent;
    varying vec4 fBitangent;

    void main(){
      vec4 viewPosition = uViewMatrix*uModelMatrix*vPosition;

      gl_Position = uProjMatrix*viewPosition;
      fPosition = viewPosition.xyz;
      fNormal = vNormal;
      fTexCoord = vTexCoord;
      fTangent = vTangent;
      fBitangent = vBitangent;
    }
  </script>

  <script id="fragment-shader" type="x-shader/x-fragment">
    precision mediump float;

    struct Light{
      vec4 position;
      vec4 ambient;
      vec4 diffuse;
      vec4 specular;
      bool enabled;
      float attenConstant;
      float attenLinear;
      float attenExp;
    };

    struct Material{
      vec4 ambient;
      vec4 diffuse;
      vec4 specular;
      float shininess;
    };
        
    uniform Light uLights[10];
    uniform Material uMaterial;
    uniform sampler2D uTextureId;
    uniform bool uTextureEnabled;
    uniform sampler2D uNormalMapId;
    uniform bool uNormalMapEnabled;
    uniform mat4 uNormalMatrix;

    varying vec3 fPosition;
    varying vec2 fTexCoord;
    varying vec4 fNormal;
    varying vec4 fTangent;
    varying vec4 fBitangent;


    vec4 calcPointLight(Light light, vec3 E, vec3 N){
      vec4 color = light.ambient * uMaterial.ambient;
      vec3 L;
      float attenuation = 1.0;

      if (light.position.w == 0.0){
        L = normalize( light.position.xyz );
      } else {
        float distance = length(light.position.xyz - fPosition);
        L = normalize( light.position.xyz/light.position.w - fPosition );
        attenuation = light.attenConstant + light.attenLinear*distance + light.attenExp*pow(distance, 2.0);
      }

      float Kd = dot(L, N);
      if (Kd > 0.0) {
        color += Kd * (light.diffuse * uMaterial.diffuse);

        vec3 R = normalize((2.0*N*dot(L,N)) - L);
        float Ks = dot(E, R);
        if (Ks > 0.0){
          Ks =  pow( Ks, uMaterial.shininess );
          color += Ks * (light.specular * uMaterial.specular);
        }
      }

      return color / attenuation;
    }


    vec3 calcBumpedNormal(){
      vec3 normal = normalize((uNormalMatrix * fNormal).xyz);
      vec3 tangent = normalize((uNormalMatrix * fTangent).xyz);
      vec3 bitangent = normalize((uNormalMatrix * fBitangent).xyz);

      vec3 bumpMapNormal = 2.0 * texture2D(uNormalMapId, fTexCoord).xyz - 1.0;

      mat3 TBN = mat3(tangent, bitangent, normal);
      vec3 newNormal = TBN * bumpMapNormal;
      newNormal = normalize(newNormal);
      return newNormal;
    }


    void main(){
      vec4 color = vec4(0.0, 0.0, 0.0, 1.0);
      vec3 N, E;

      if(uNormalMapEnabled){
        N = calcBumpedNormal();
      } else {
        N = normalize((uNormalMatrix * fNormal).xyz);
      }

      E = normalize(-fPosition);

      for (int i = 0; i < 10; i++){
        if (uLights[i].enabled){
          color += calcPointLight(uLights[i], E, N);
        }
      }

      vec4 finalColor = vec4(color.xyz, 1.0);
      if (uTextureEnabled){ finalColor *= texture2D(uTextureId, fTexCoord); }
      gl_FragColor = finalColor;
    }
  </script>

  <link rel="stylesheet" type="text/css" href="css/main.css">

  <script type="text/javascript" src="../../common/webgl_utils.js"></script>
  <script type="text/javascript" src="../../common/init_shaders.js"></script>
  <script type="text/javascript" src="../../common/mv.js"></script>
  <script type="text/javascript" src="js/meshes/cube.js"></script>
  <script type="text/javascript" src="js/meshes/sphere.js"></script>
  <script type="text/javascript" src="js/meshes/skybox.js"></script>
  <script type="text/javascript" src="js/renderer.js"></script>
  <script type="text/javascript" src="js/mesh.js"></script>
  <script type="text/javascript" src="js/texture.js"></script>
  <script type="text/javascript" src="js/quaternion.js"></script>
  <script type="text/javascript" src="js/events_engine.js"></script>
  <script type="text/javascript" src="js/camera.js"></script>
  <script type="text/javascript" src="js/light_source.js"></script>
  <script type="text/javascript" src="js/instance.js"></script>
  <script type="text/javascript" src="js/scene.js"></script>
  <script type="text/javascript" src="js/main.js"></script>
</head>
<body>
  <a href='../../index.html'>Back</a> | <a href='https://github.com/Vizvamitra/webgl_study/tree/master/8/01_textures'>Github</a><br><br>

  <div class='container'>
    <canvas id='gl-canvas' width=300 height=300>
      Canvas is not supported by your browser...
    </canvas>

    <form class='controls'>

      <fieldset>
        <label>Normal mapping: </label>
        <input type='checkbox' name='normalMapping' checked onchange='onCheckboxSwitch(event)'/>
      </fieldset>

      <!-- <fieldset class='ranges'>
        <label for='rotationSpeed'>Lights rotation speed</label>
        <output class='rotationSpeed'>1</output>
        <input type='range' name='rotationSpeed' min=0 max=5 step=0.1 value=1 oninput='onRotationSpeedChange(event)'><br>
      </fieldset>

      <fieldset>
        <label style='vertical-align: top;'>Lights rotation axis:</label>
        <select id='axis' name='axis' size=3 onchange='onRotationAxisChange(event);'>
          <option value='0'> x </option>
          <option selected value='1'> y </option>
          <option value='2'> z </option>
        </select><br><br>

        <button onclick='onRotationDirectionChange(event);'>Change rotation direction</button>
      </fieldset> -->
    </form>

  </div>

  <div id='info'>
    <p>Be patient please, scene may need time to load</p>
    <p>Image is rotatable with mouse</p>
    <p>Use mouse wheel to zoom in/out</p>
  </div>

</body>
</html>